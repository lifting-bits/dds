#import "header.dr"

;---------------------------------------------------------
get_instructions(EA) 
    : instruction(EA, _, _).

;---------------------------------------------------------
plausible_instruction(EA)
    : instruction(EA, _, _).

plausible_instruction(EA)
    : external_symbol(EA, _)
    , raw_transfer(_, EA, _).

;---------------------------------------------------------
; Fall-throughs to non-decodable instructions.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_NORMAL, _)
    , raw_transfer(EA, ToEA, EDGE_FALL_THROUGH)
    , !plausible_instruction(ToEA).

; Direct jumps to non-decodable instructions.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_DIRECT_JUMP, _)
    , raw_transfer(EA, ToEA, EDGE_JUMP_TAKEN)
    , !plausible_instruction(ToEA).

; Direct calls to non-decodable instructions. We omit 
; return edges since the target function may not return.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_DIRECT_CALL, _)
    , raw_transfer(EA, ToEA, EDGE_FUNCTION_CALL)
    , !plausible_instruction(ToEA).

; Conditional direct jumps where both edges precede 
; non-decodable instructions.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_COND_DIRECT_JUMP, _)
    , raw_transfer(EA, TargEA, EDGE_JUMP_TAKEN)
    , raw_transfer(EA, FallEA, EDGE_JUMP_NOT_TAKEN)
    , !plausible_instruction(TargEA)
    , !plausible_instruction(FallEA).

; Conditional direct calls where both edges precede 
; non-decodable instructions.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_COND_DIRECT_CALL, _)
    , raw_transfer(EA, TargEA, EDGE_FUNCTION_CALL)
    , raw_transfer(EA, FallEA, EDGE_FUNCTION_CALL_RETURN)
    , !plausible_instruction(TargEA)
    , !plausible_instruction(FallEA).

; Recursive case for fall-throughs.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_NORMAL, _)
    , raw_transfer(EA, ToEA, EDGE_FALL_THROUGH)
    , dominates_invalid_instruction(ToEA).

; Recursive case for direct jumps.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_DIRECT_JUMP, _)
    , raw_transfer(EA, ToEA, EDGE_JUMP_TAKEN)
    , dominates_invalid_instruction(ToEA).

; Recursive case for conditional direct jumps.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_COND_DIRECT_JUMP, _)
    , raw_transfer(EA, ToEA, EDGE_JUMP_TAKEN)
    , dominates_invalid_instruction(ToEA).

; Recursive case for direct calls.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_DIRECT_CALL, _)
    , raw_transfer(EA, ToEA, EDGE_FUNCTION_CALL)
    , dominates_invalid_instruction(ToEA).

; Recursive case for conditional direct calls.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_DI, _)
    , raw_transfer(EA, ToEA, EDGE_FUNCTION_CALL)
    , dominates_invalid_instruction(ToEA).

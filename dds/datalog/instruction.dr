; Copyright 2021, Trail of Bits. All rights reserved.

#import "header.dr"

;---------------------------------------------------------
get_instructions(EA) 
    : instruction(EA, _, _).

;---------------------------------------------------------
plausible_instruction(EA)
    : instruction(EA, _, _).

plausible_instruction(EA)
    : external_symbol(EA, _)
    , raw_transfer(_, EA, _).

;---------------------------------------------------------
; Fall-throughs to non-decodable instructions.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_NORMAL, _)
    , raw_transfer(EA, FallThroughEA, EDGE_FALL_THROUGH)
    , !plausible_instruction(FallThroughEA).

; Direct jumps to non-decodable instructions.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_DIRECT_JUMP, _)
    , raw_transfer(EA, JmpTargetEA, EDGE_JUMP_TAKEN)
    , !plausible_instruction(JmpTargetEA).

; Direct calls to non-decodable instructions. We omit 
; return edges since the target function may not return.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_DIRECT_CALL, _)
    , raw_transfer(EA, CalleeEA, EDGE_FUNCTION_CALL)
    , !plausible_instruction(CalleeEA).

; Conditional direct jumps where both edges precede 
; non-decodable instructions.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_COND_DIRECT_JUMP, _)
    , raw_transfer(EA, TakenEA, EDGE_JUMP_TAKEN)
    , raw_transfer(EA, NotTakenEA, EDGE_JUMP_NOT_TAKEN)
    , !plausible_instruction(TakenEA)
    , !plausible_instruction(NotTakenEA).

; Conditional direct calls where both edges precede 
; non-decodable instructions.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_COND_DIRECT_CALL, _)
    , raw_transfer(EA, CalleeEA, EDGE_FUNCTION_CALL)
    , raw_transfer(EA, ReturnEA, EDGE_FUNCTION_CALL_RETURN)
    , !plausible_instruction(CalleeEA)
    , !plausible_instruction(ReturnEA).

; Recursive case for fall-throughs.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_NORMAL, _)
    , raw_transfer(EA, FallThroughEA, EDGE_FALL_THROUGH)
    , dominates_invalid_instruction(FallThroughEA).

; Recursive case for direct jumps.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_DIRECT_JUMP, _)
    , raw_transfer(EA, JmpTargetEA, EDGE_JUMP_TAKEN)
    , dominates_invalid_instruction(JmpTargetEA).

; Recursive case for conditional direct jumps.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_COND_DIRECT_JUMP, _)
    , raw_transfer(EA, TakenEA, EDGE_JUMP_TAKEN)
    , raw_transfer(EA, NotTakenEA, EDGE_JUMP_NOT_TAKEN)
    , dominates_invalid_instruction(TakenEA)
    , dominates_invalid_instruction(NotTakenEA).

; Recursive case for direct calls.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_DIRECT_CALL, _)
    , raw_transfer(EA, CalleeEA, EDGE_FUNCTION_CALL)
    , dominates_invalid_instruction(CalleeEA).

; Recursive case for conditional direct calls.
dominates_invalid_instruction(EA)
    : instruction(EA, INSN_COND_DIRECT_CALL, _)
    , raw_transfer(EA, CalleeEA, EDGE_FUNCTION_CALL)
    , raw_transfer(EA, RetEA, EDGE_FUNCTION_CALL_RETURN)
    , dominates_invalid_instruction(CalleeEA)
    , dominates_invalid_instruction(RetEA).

#import "header.dr"

;---------------------------------------------------------
; Treat interprocedrual transfer targets as function heads.
function(FuncEA)
  : transfer(_, FuncEA, EDGE_CALL_TARG).

; Treat the binary's entrypoint as a function head.
function(EntryPointEA)
  : entrypoint(EntryPointEA)
  , plausible_instruction(EntryPointEA).

; Parse constructor/destructor, imported/exported, and local 
; function heads.
function(FuncEA)
  : constructor_function(FuncEA)
  , instruction(FuncEA, _, _).
function(FuncEA)
  : destructor_function(FuncEA)
  , instruction(FuncEA, _, _).
function(FuncEA)
  : imported_function(FuncEA, _)
  , instruction(FuncEA, _, _).
function(FuncEA)
  : exported_function(FuncEA, _)
  , instruction(FuncEA, _, _).
function(FuncEA)
  : local_function(FuncEA, _)
  , instruction(FuncEA, _, _).

; Treat imported, exported, and local symbols pointing to valid 
; instructions as function heads.
function(FuncEA)
  : imported_symbol(FuncEA, _)
  , instruction(FuncEA, _, _).
function(FuncEA)
  : exported_symbol(FuncEA, _)
  , instruction(FuncEA, _, _).
function(FuncEA)
  : local_symbol(FuncEA, _)
  , instruction(FuncEA, _, _).

; Any type of control-flow transfer to an external is treated
; as a making that external into a function.
function(FuncEA)
  : external_symbol(FuncEA, _)
  , transfer(_, FuncEA, _).

; An indirect jump in the `.plt` that has a been rewritten
; as an external transfer is treated as a function head.
function(FuncEA)
  : instruction(FuncEA, INSN_INDIRECT_JUMP, _)
  , transfer(FuncEA, ExternEA, EDGE_JUMP_TARG)
  , external_symbol(ExternEA, _).

;---------------------------------------------------------
; If an instruction begins a function, it is part of it.
function_instruction(FuncEA, FuncEA)
  : function(FuncEA).

; A function's intraproc-connected instructions are part of it.
function_instruction(FuncEA, NextEA)
  : function_instruction(FuncEA, InsnEA)
  , intraproc_transfer(InsnEA, NextEA).

  